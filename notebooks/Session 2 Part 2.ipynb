{
 "nbformat_minor": 0, 
 "nbformat": 4, 
 "cells": [
  {
   "source": [
    "# Session 2 Part 2\n", 
    "\n", 
    "Now we need to change the simulation tasks to use the partitions we've created. There are two steps to this process:\n", 
    "\n", 
    "First, the field space declarations will need to change to accomodate the new partitions. Remember that the nodes have been partitioned three ways: into private, shared, and ghost subregions (example reproduced below). This means that a task which wants to operate for example on the red nodes will need three regions (private, shared, and ghost). This also means that any fspace which points to nodes must be updated to take three arguments as well.\n", 
    "\n", 
    "<table style=\"border: 0px;\">\n", 
    "<tr style=\"border: 0px;\">\n", 
    "<td style=\"border: 0px; padding: 10px;\">\n", 
    "Private (Partition of Nodes)\n", 
    "<img src=\"/images/circuit/partition5_private.png\" width=\"250\">\n", 
    "</td>\n", 
    "<td style=\"border: 0px; padding: 10px;\">\n", 
    "Shared (Partition of Nodes)\n", 
    "<img src=\"/images/circuit/partition6_shared.png\" width=\"250\">\n", 
    "</td>\n", 
    "<td style=\"border: 0px; padding: 10px;\">\n", 
    "Ghost (Partition of Nodes)\n", 
    "<img src=\"/images/circuit/partition7_ghost.png\" width=\"250\">\n", 
    "</td>\n", 
    "</tr>\n", 
    "</table>\n", 
    "\n", 
    "After this is done, the rest is straightforward: change each task call in the main simulation loop to operate over subregions of the partitions. (Hint: Use a for loop.)\n", 
    "\n", 
    "## Syntax Guide\n"
   ], 
   "cell_type": "markdown", 
   "metadata": {}
  }, 
  {
   "execution_count": null, 
   "cell_type": "code", 
   "source": [
    "ptr(T, R1, R2, R3) -- Pointer types may point to multiple regions.\n", 
    "P[I] -- Returns the Ith subregion of the partition P."
   ], 
   "outputs": [], 
   "metadata": {
    "collapsed": false
   }
  }, 
  {
   "source": [
    "## Exercise"
   ], 
   "cell_type": "markdown", 
   "metadata": {}
  }, 
  {
   "execution_count": null, 
   "cell_type": "code", 
   "source": [
    "import \"regent\"\n", 
    "\n", 
    "local c = regentlib.c\n", 
    "\n", 
    "struct Currents {\n", 
    "  _0 : float,\n", 
    "  _1 : float,\n", 
    "  _2 : float,\n", 
    "}\n", 
    "\n", 
    "struct Voltages {\n", 
    "  _1 : float,\n", 
    "  _2 : float,\n", 
    "}\n", 
    "\n", 
    "fspace Node {\n", 
    "  capacitance : float,\n", 
    "  leakage     : float,\n", 
    "  charge      : float,\n", 
    "  voltage     : float,\n", 
    "}\n", 
    "\n", 
    "-- TODO: Change Wire to take three arguments: private, shared, and\n", 
    "-- ghost nodes.\n", 
    "fspace Wire(rn : region(Node)) {\n", 
    "  in_node     : ptr(Node, rn),\n", 
    "  out_node    : ptr(Node, rn),\n", 
    "  inductance  : float,\n", 
    "  resistance  : float,\n", 
    "  capacitance : float,\n", 
    "  current     : Currents,\n", 
    "  voltage     : Voltages,\n", 
    "}\n", 
    "\n", 
    "local CktConfig = require(\"session1/circuit_config\")\n", 
    "local helper = require(\"session2/circuit_helper\")\n", 
    "local validator = require(\"session2/circuit_validator\")\n", 
    "\n", 
    "local WS = 3\n", 
    "local dT = 1e-7\n", 
    "\n", 
    "-- TODO: Change this task to take three regions of nodes (private,\n", 
    "-- shared, and ghost).\n", 
    "task calculate_new_currents(steps : uint,\n", 
    "                            rn : region(Node),\n", 
    "                            rw : region(Wire(rn)))\n", 
    "where\n", 
    "  reads(rn.voltage,\n", 
    "        rw.{in_node, out_node, inductance, resistance, capacitance}),\n", 
    "  reads writes(rw.{current, voltage})\n", 
    "do\n", 
    "  var rdT : float = 1.0 / dT\n", 
    "  __demand(__vectorize)\n", 
    "  for w in rw do\n", 
    "    var temp_v : float[WS + 1]\n", 
    "    var temp_i : float[WS]\n", 
    "    var old_i : float[WS]\n", 
    "    var old_v : float[WS - 1]\n", 
    "\n", 
    "    temp_i[0] = w.current._0\n", 
    "    temp_i[1] = w.current._1\n", 
    "    temp_i[2] = w.current._2\n", 
    "    for i = 0, WS do old_i[i] = temp_i[i] end\n", 
    "\n", 
    "    temp_v[1] = w.voltage._1\n", 
    "    temp_v[2] = w.voltage._2\n", 
    "    for i = 0, WS - 1 do old_v[i] = temp_v[i + 1] end\n", 
    "\n", 
    "    -- Pin the outer voltages to the node voltages.\n", 
    "    temp_v[0] = w.in_node.voltage\n", 
    "    temp_v[WS] = w.out_node.voltage\n", 
    "\n", 
    "    -- Solve the RLC model iteratively.\n", 
    "    var L : float = w.inductance\n", 
    "    var rR : float = 1.0 / w.resistance\n", 
    "    var rC : float = 1.0 / w.capacitance\n", 
    "    for s = 1, steps + 1 do\n", 
    "      -- First, figure out the new current from the voltage differential\n", 
    "      -- and our inductance:\n", 
    "      -- dV = R*I + L*I' ==> I = (dV - L*I')/R\n", 
    "      for i = 0, WS do\n", 
    "        temp_i[i] = ((temp_v[i + 1] - temp_v[i]) -\n", 
    "                     (L * (temp_i[i] - old_i[i]) * rdT)) * rR\n", 
    "      end\n", 
    "      -- Now update the inter-node voltages.\n", 
    "      for i = 0, WS - 1 do\n", 
    "        temp_v[i + 1] = old_v[i] + dT * (temp_i[i] - temp_i[i + 1]) * rC\n", 
    "      end\n", 
    "    end\n", 
    "\n", 
    "    -- Write out the results.\n", 
    "    w.current._0 = temp_i[0]\n", 
    "    w.current._1 = temp_i[1]\n", 
    "    w.current._2 = temp_i[2]\n", 
    "\n", 
    "    w.voltage._1 = temp_v[1]\n", 
    "    w.voltage._2 = temp_v[2]\n", 
    "  end\n", 
    "end\n", 
    "\n", 
    "-- TODO: Change this task to take three regions of nodes (private,\n", 
    "-- shared, and ghost).\n", 
    "task distribute_charge(rn : region(Node),\n", 
    "                       rw : region(Wire(rn)))\n", 
    "where\n", 
    "  reads(rw.{in_node, out_node, current._0, current._2}),\n", 
    "  reduces +(rn.charge)\n", 
    "do\n", 
    "  for w in rw do\n", 
    "    var in_current = -dT * w.current._0\n", 
    "    var out_current = dT * w.current._2\n", 
    "    w.in_node.charge += in_current\n", 
    "    w.out_node.charge += out_current\n", 
    "  end\n", 
    "end\n", 
    "\n", 
    "task update_voltages(rn : region(Node))\n", 
    "where\n", 
    "  reads(rn.{capacitance, leakage}),\n", 
    "  reads writes(rn.{voltage, charge})\n", 
    "do\n", 
    "  for n in rn do\n", 
    "    var voltage = n.voltage + n.charge / n.capacitance\n", 
    "    voltage = voltage * (1.0 - n.leakage)\n", 
    "    n.voltage = voltage\n", 
    "    n.charge = 0.0\n", 
    "  end\n", 
    "end\n", 
    "\n", 
    "task toplevel()\n", 
    "  var conf : CktConfig\n", 
    "  conf:initialize_from_command()\n", 
    "  conf:show()\n", 
    "\n", 
    "  var num_circuit_nodes = conf.num_pieces * conf.nodes_per_piece\n", 
    "  var num_circuit_wires = conf.num_pieces * conf.wires_per_piece\n", 
    "\n", 
    "  var rn = region(ispace(ptr, num_circuit_nodes), Node)\n", 
    "  var rw = region(ispace(ptr, num_circuit_wires), Wire(wild))\n", 
    "\n", 
    "  new(ptr(Node, rn), num_circuit_nodes)\n", 
    "  new(ptr(Wire(wild), rw), num_circuit_wires)\n", 
    "\n", 
    "  c.printf(\"Generating a random circuit...\\n\")\n", 
    "  helper.generate_random_circuit(rn, rw, conf)\n", 
    "\n", 
    "  var colors = ispace(int1d, conf.num_pieces)\n", 
    "  var pn_equal = partition(equal, rn, colors)\n", 
    "  var pw = preimage(rw, pn_equal, rw.in_node)\n", 
    "  var pn_extrefs = image(rn, preimage(rw, pn_equal, rw.out_node) - pw, rw.out_node)\n", 
    "  var pn_private = pn_equal - pn_extrefs\n", 
    "  var pn_shared = pn_equal & pn_extrefs\n", 
    "  var pn_ghost = image(rn, pw, rw.out_node) - pn_equal\n", 
    "\n", 
    "  helper.dump_graph(conf, rn, rw)\n", 
    "  for i = 0, conf.num_pieces do\n", 
    "    helper.initialize_pointers(pn_private[i], pn_shared[i], pn_ghost[i], pw[i])\n", 
    "  end\n", 
    "\n", 
    "  helper.wait_for(helper.block(rn, rw))\n", 
    "  c.printf(\"Starting main simulation loop\\n\")\n", 
    "  var ts_start = helper.timestamp()\n", 
    "\n", 
    "  for j = 0, conf.num_loops do\n", 
    "    -- TODO: Change each of these calls to work on a piece of the\n", 
    "    -- graph.\n", 
    "    calculate_new_currents(conf.steps, rn, rw)\n", 
    "    distribute_charge(rn, rw)\n", 
    "    update_voltages(rn)\n", 
    "  end\n", 
    "\n", 
    "  -- Wait for all previous tasks to complete and measure the elapsed time.\n", 
    "  var _ = 0\n", 
    "  for i = 0, conf.num_pieces do\n", 
    "    _ += helper.block(pn_equal[i], pw[i])\n", 
    "  end\n", 
    "  helper.wait_for(_)\n", 
    "  var ts_end = helper.timestamp()\n", 
    "  c.printf(\"simulation complete\\n\")\n", 
    "\n", 
    "  var sim_time = 1e-6 * (ts_end - ts_start)\n", 
    "  c.printf(\"ELAPSED TIME = %7.3f s\\n\", sim_time)\n", 
    "  var gflops =\n", 
    "    helper.calculate_gflops(sim_time, WS * 6 + (WS - 1) * 4, 4, 4, conf)\n", 
    "  c.printf(\"GFLOPS = %7.3f GFLOPS\\n\", gflops)\n", 
    "\n", 
    "  c.printf(\"Validating simulation results...\\n\")\n", 
    "  validator.validate_solution(rn, rw, conf)\n", 
    "end\n", 
    "regentlib.start(toplevel)\n"
   ], 
   "outputs": [], 
   "metadata": {
    "collapsed": false
   }
  }, 
  {
   "source": [
    "That's it, you've finished the simulation! Hope you've enjoyed the exercise.\n", 
    "\n", 
    "Note: There is another session which covers ([mapping the computation onto GPUs](Session 3 Part 1.ipynb)). However, this machine is not equipped with GPUs, so you won't be able to follow the instructions and see it run. Feel free to take a look at play around with it, if you're still interested."
   ], 
   "cell_type": "markdown", 
   "metadata": {}
  }
 ], 
 "metadata": {
  "kernelspec": {
   "display_name": "Regent", 
   "name": "regent", 
   "language": "regent"
  }, 
  "language_info": {
   "mimetype": "text/x-regent", 
   "pygments_lexer": "regent", 
   "file_extension": "rg", 
   "name": "regent"
  }
 }
}