{
 "nbformat_minor": 0, 
 "nbformat": 4, 
 "cells": [
  {
   "source": [
    "# Session 3 Part 1\n", 
    "\n", 
    "In this sesseion, you make the circuit simulation run on GPUs. We are going to see that mapping decisions do not interfere with the correctness of the application (modulo mapping failures from incorrect mapping decisions).\n", 
    "\n", 
    "The task of this exercise is to write some mapping rules to map the three simulation tasks to GPUs. To write mapping rules, you use a new mapper language called Bishop. In the syntax guide below, you can find how you can select a set of tasks (regions) and set their target to a particular processor (memory). We are giving you a mapping rule for task 'update_voltages' that maps the task to CPUs (more precisely, processors that supports x86 ISA). Based on this example and the syntax guide given below, you write\n", 
    "\n", 
    "- Mapping rules that map tasks `calculate_new_currents`, `distribute_charges`, and `update_voltages` to GPUs (i.e. processors that support CUDA ISA)\n", 
    "- Mapping rule(s) that map regions of these tasks to a zero-copy memory that is visible to the processor that the tasks have been mapped to.\n", 
    "- As a bonus point, mapping rules that map task `distribute_charges` to even numbered GPUs and task `update_voltages` to odd numbered GPUs (you can assume that there are even number of GPUs).\n", 
    "\n", 
    "With all your mapping rules written correctly, you should see the solution passes validation.\n", 
    "\n", 
    "## Syntax Guide\n"
   ], 
   "cell_type": "markdown", 
   "metadata": {}
  }, 
  {
   "execution_count": null, 
   "cell_type": "code", 
   "source": [
    "__demand(__cuda) task T ... -- Generates both x86 and CUDA variants for task T\n", 
    "bishop ... end              -- Starts a bishop mapper\n", 
    "TE    { target : V; }       -- Sets value V as the target of a task that matches TE\n", 
    "TE RE { target : V; }       -- Sets value V as the target of a region that matches RE and whose task matches TE\n", 
    "\n", 
    "-- Task Element (TE)\n", 
    "task                        -- Selects any tasks\n", 
    "task#T                      -- Selects tasks named T\n", 
    "task[isa=I]                 -- Selects tasks mapped to a processor that supports ISA I\n", 
    "TE[target=$T]               -- Selects tasks that satisfy TE and then binds their target to $T\n", 
    "TE[index=$P]                -- Selects tasks that satisfy TE and then binds their point in the launch domain to $P\n", 
    "\n", 
    "-- Region Element (RE)\n", 
    "region                      -- Selects any regions\n", 
    "region#P                    -- Selects regions named P in the signature\n", 
    "\n", 
    "-- Processor objects\n", 
    "processors                  -- A list of processors in the whole system\n", 
    "processors[isa=I]           -- A list of processors that support ISA I (either x86 or cuda)\n", 
    "processors[N]               -- The N-th processor in the list\n", 
    "L.size                      -- The size of list L of processors\n", 
    "P.memories                  -- A list of memories visible to processor P\n", 
    "\n", 
    "-- Memory objects\n", 
    "memories                    -- A list of memories in the whole system\n", 
    "memories[kind=K]            -- A list of memories of kind K (sysmem, regmem, fbmem, or zcmem)\n", 
    "memories[N]                 -- The N-th memory in the list\n", 
    "L.size                      -- The size of list L of memories\n", 
    "\n", 
    "-- Expressions for list indices\n", 
    "$P[0]                       -- The first coordinate of point $P\n", 
    "E1 + E2, E1 - E2, E1 * E2, E1 / E2, E1 % E2 -- Usual integer arithmetic expressions\n"
   ], 
   "outputs": [], 
   "metadata": {
    "collapsed": false
   }
  }, 
  {
   "source": [
    "## Exercise"
   ], 
   "cell_type": "markdown", 
   "metadata": {}
  }, 
  {
   "execution_count": null, 
   "cell_type": "code", 
   "source": [
    "import \"regent\"\n", 
    "import \"bishop\"\n", 
    "\n", 
    "local c = regentlib.c\n", 
    "\n", 
    "struct Currents {\n", 
    "  _0 : float,\n", 
    "  _1 : float,\n", 
    "  _2 : float,\n", 
    "}\n", 
    "\n", 
    "struct Voltages {\n", 
    "  _1 : float,\n", 
    "  _2 : float,\n", 
    "}\n", 
    "\n", 
    "fspace Node {\n", 
    "  capacitance : float,\n", 
    "  leakage     : float,\n", 
    "  charge      : float,\n", 
    "  voltage     : float,\n", 
    "}\n", 
    "\n", 
    "fspace Wire(rpn : region(Node), rsn : region(Node), rgn : region(Node)) {\n", 
    "  in_node     : ptr(Node, rpn, rsn),\n", 
    "  out_node    : ptr(Node, rpn, rsn, rgn),\n", 
    "  inductance  : float,\n", 
    "  resistance  : float,\n", 
    "  capacitance : float,\n", 
    "  current     : Currents,\n", 
    "  voltage     : Voltages,\n", 
    "}\n", 
    "\n", 
    "local CktConfig = require(\"session1/circuit_config\")\n", 
    "local helper = require(\"session2/circuit_helper\")\n", 
    "local validator = require(\"session2/circuit_validator\")\n", 
    "\n", 
    "local WS = 3\n", 
    "local dT = 1e-7\n", 
    "\n", 
    "mapper\n", 
    "\n", 
    "task#update_voltages\n", 
    "{\n", 
    "  target : processors[isa=x86];\n", 
    "}\n", 
    "\n", 
    "-- TODO: Write mapping rules that map the three simulation tasks to GPUs.\n", 
    "--       You might also want to try solving the bonus problem above.\n", 
    "task\n", 
    "{\n", 
    "  target : ;\n", 
    "}\n", 
    "\n", 
    "-- TODO: Write mapping rules that map regions of the three simulation tasks to a zero-copy memory.\n", 
    "task region\n", 
    "{\n", 
    "  target : ;\n", 
    "}\n", 
    "\n", 
    "end\n", 
    "\n", 
    "__demand(__cuda)\n", 
    "task calculate_new_currents(steps : uint,\n", 
    "                            rpn : region(Node),\n", 
    "                            rsn : region(Node),\n", 
    "                            rgn : region(Node),\n", 
    "                            rw : region(Wire(rpn, rsn, rgn)))\n", 
    "where\n", 
    "  reads(rpn.voltage, rsn.voltage, rgn.voltage,\n", 
    "        rw.{in_node, out_node, inductance, resistance, capacitance}),\n", 
    "  reads writes(rw.{current, voltage})\n", 
    "do\n", 
    "  var rdT : float = 1.0 / dT\n", 
    "  __demand(__vectorize)\n", 
    "  for w in rw do\n", 
    "    var temp_v : float[WS + 1]\n", 
    "    var temp_i : float[WS]\n", 
    "    var old_i : float[WS]\n", 
    "    var old_v : float[WS - 1]\n", 
    "\n", 
    "    temp_i[0] = w.current._0\n", 
    "    temp_i[1] = w.current._1\n", 
    "    temp_i[2] = w.current._2\n", 
    "    for i = 0, WS do old_i[i] = temp_i[i] end\n", 
    "\n", 
    "    temp_v[1] = w.voltage._1\n", 
    "    temp_v[2] = w.voltage._2\n", 
    "    for i = 0, WS - 1 do old_v[i] = temp_v[i + 1] end\n", 
    "\n", 
    "    -- Pin the outer voltages to the node voltages.\n", 
    "    temp_v[0] = w.in_node.voltage\n", 
    "    temp_v[WS] = w.out_node.voltage\n", 
    "\n", 
    "    -- Solve the RLC model iteratively.\n", 
    "    var L : float = w.inductance\n", 
    "    var rR : float = 1.0 / w.resistance\n", 
    "    var rC : float = 1.0 / w.capacitance\n", 
    "    for j = 0, steps do\n", 
    "      -- First, figure out the new current from the voltage differential\n", 
    "      -- and our inductance:\n", 
    "      -- dV = R*I + L*I' ==> I = (dV - L*I')/R\n", 
    "      for i = 0, WS do\n", 
    "        temp_i[i] = ((temp_v[i + 1] - temp_v[i]) -\n", 
    "                     (L * (temp_i[i] - old_i[i]) * rdT)) * rR\n", 
    "      end\n", 
    "      -- Now update the inter-node voltages.\n", 
    "      for i = 0, WS - 1 do\n", 
    "        temp_v[i + 1] = old_v[i] + dT * (temp_i[i] - temp_i[i + 1]) * rC\n", 
    "      end\n", 
    "    end\n", 
    "\n", 
    "    -- Write out the results.\n", 
    "    w.current._0 = temp_i[0]\n", 
    "    w.current._1 = temp_i[1]\n", 
    "    w.current._2 = temp_i[2]\n", 
    "\n", 
    "    w.voltage._1 = temp_v[1]\n", 
    "    w.voltage._2 = temp_v[2]\n", 
    "  end\n", 
    "end\n", 
    "\n", 
    "__demand(__cuda)\n", 
    "task distribute_charge(rpn : region(Node),\n", 
    "                       rsn : region(Node),\n", 
    "                       rgn : region(Node),\n", 
    "                       rw : region(Wire(rpn, rsn, rgn)))\n", 
    "where\n", 
    "  reads(rw.{in_node, out_node, current._0, current._2}),\n", 
    "  reduces +(rpn.charge, rsn.charge, rgn.charge)\n", 
    "do\n", 
    "  for w in rw do\n", 
    "    var in_current = -dT * w.current._0\n", 
    "    var out_current = dT * w.current._2\n", 
    "    w.in_node.charge += in_current\n", 
    "    w.out_node.charge += out_current\n", 
    "  end\n", 
    "end\n", 
    "\n", 
    "__demand(__cuda)\n", 
    "task update_voltages(rn : region(Node))\n", 
    "where\n", 
    "  reads(rn.{capacitance, leakage}),\n", 
    "  reads writes(rn.{voltage, charge})\n", 
    "do\n", 
    "  for n in rn do\n", 
    "    var voltage = n.voltage + n.charge / n.capacitance\n", 
    "    voltage = voltage * (1.0 - n.leakage)\n", 
    "    n.voltage = voltage\n", 
    "    n.charge = 0.0\n", 
    "  end\n", 
    "end\n", 
    "\n", 
    "task toplevel()\n", 
    "  var conf : CktConfig\n", 
    "  conf:initialize_from_command()\n", 
    "  conf:show()\n", 
    "\n", 
    "  var num_circuit_nodes = conf.num_pieces * conf.nodes_per_piece\n", 
    "  var num_circuit_wires = conf.num_pieces * conf.wires_per_piece\n", 
    "\n", 
    "  var rn = region(ispace(ptr, num_circuit_nodes), Node)\n", 
    "  var rw = region(ispace(ptr, num_circuit_wires), Wire(wild, wild, wild))\n", 
    "\n", 
    "  new(ptr(Node, rn), num_circuit_nodes)\n", 
    "  new(ptr(Wire(wild, wild, wild), rw), num_circuit_wires)\n", 
    "\n", 
    "  c.printf(\"Generating a random circuit...\\n\")\n", 
    "  helper.generate_random_circuit(rn, rw, conf)\n", 
    "\n", 
    "  var colors = ispace(int1d, conf.num_pieces)\n", 
    "  var pn_equal = partition(equal, rn, colors)\n", 
    "  var pw = preimage(rw, pn_equal, rw.in_node)\n", 
    "  var pn_extrefs = image(rn, preimage(rw, pn_equal, rw.out_node) - pw, rw.out_node)\n", 
    "  var pn_private = pn_equal - pn_extrefs\n", 
    "  var pn_shared = pn_equal & pn_extrefs\n", 
    "  var pn_ghost = image(rn, pw, rw.out_node) - pn_equal\n", 
    "\n", 
    "  __demand(__parallel)\n", 
    "  for i = 0, conf.num_pieces do\n", 
    "    helper.initialize_pointers(pn_private[i], pn_shared[i], pn_ghost[i], pw[i])\n", 
    "  end\n", 
    "\n", 
    "  helper.wait_for(helper.block(rn, rw))\n", 
    "\n", 
    "  --helper.dump_graph(conf, rn, rw)\n", 
    "\n", 
    "  c.printf(\"Starting main simulation loop\\n\")\n", 
    "  var ts_start = helper.timestamp()\n", 
    "\n", 
    "  for j = 0, conf.num_loops do\n", 
    "    for i = 0, conf.num_pieces do\n", 
    "      calculate_new_currents(conf.steps, pn_private[i], pn_shared[i], pn_ghost[i], pw[i])\n", 
    "    end\n", 
    "    for i = 0, conf.num_pieces do\n", 
    "      distribute_charge(pn_private[i], pn_shared[i], pn_ghost[i], pw[i])\n", 
    "    end\n", 
    "    for i = 0, conf.num_pieces do\n", 
    "      update_voltages(pn_equal[i])\n", 
    "    end\n", 
    "  end\n", 
    "\n", 
    "  -- Wait for all previous tasks to complete and measure the elapsed time.\n", 
    "  var _ = 0\n", 
    "  for i = 0, conf.num_pieces do\n", 
    "    _ += helper.block(pn_equal[i], pw[i])\n", 
    "  end\n", 
    "  helper.wait_for(_)\n", 
    "  var ts_end = helper.timestamp()\n", 
    "  c.printf(\"simulation complete\\n\")\n", 
    "\n", 
    "  var sim_time = 1e-6 * (ts_end - ts_start)\n", 
    "  c.printf(\"ELAPSED TIME = %7.3f s\\n\", sim_time)\n", 
    "  var gflops =\n", 
    "    helper.calculate_gflops(sim_time, WS * 6 + (WS - 1) * 4, 4, 4, conf)\n", 
    "  c.printf(\"GFLOPS = %7.3f GFLOPS\\n\", gflops)\n", 
    "\n", 
    "  c.printf(\"Validating simulation results...\\n\")\n", 
    "  validator.validate_solution(rn, rw, conf)\n", 
    "end\n", 
    "bishoplib.register_bishop_mappers()\n", 
    "regentlib.start(toplevel)\n"
   ], 
   "outputs": [], 
   "metadata": {
    "collapsed": false
   }
  }, 
  {
   "source": [
    "Next up: [try GPU mapping on a larger problem size](Session 3 Part 2.ipynb)."
   ], 
   "cell_type": "markdown", 
   "metadata": {}
  }
 ], 
 "metadata": {
  "kernelspec": {
   "display_name": "Regent", 
   "name": "regent", 
   "language": "regent"
  }, 
  "language_info": {
   "mimetype": "text/x-regent", 
   "pygments_lexer": "regent", 
   "file_extension": "rg", 
   "name": "regent"
  }
 }
}